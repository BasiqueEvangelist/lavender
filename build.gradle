plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_base_version}"
group = project.maven_group

sourceSets {
    testmod {
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.compileClasspath
    }
}

loom {
    accessWidenerPath = project.file("src/main/resources/lavender.accesswidener")

    runs {
        testmodClient {
            client()
            ideConfigGenerated true
            name "Testmod Client"
            source sourceSets.testmod

            mods {
                create("lavender_flower") {
                    sourceSet sourceSets.testmod
                }
                create("lavender"){
                    sourceSet sourceSets.main
                }
            }
        }

        testmodServer {
            server()
            ideConfigGenerated true
            name "Testmod Server"
            source sourceSets.testmod

            mods {
                create("lavender_flower") {
                    sourceSet sourceSets.testmod
                }
                create("lavender"){
                    sourceSet sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url "https://maven.wispforest.io/releases/" }

    // rei
    maven { url "https://maven.shedaniel.me/" }

    // modmenu
    maven { url "https://maven.terraformersmc.com/releases/" }

    maven { url 'https://maven.neoforged.net/releases/' }

    mavenLocal()
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
        it.mappings("dev.architectury:yarn-mappings-patch-neoforge:1.21+build.4")
    }
//    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

//    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    neoForge "net.neoforged:neoforge:${project.neoforge_version}"

    modImplementation "io.wispforest:owo-lib-neo:${project.owo_version}"
//    include "io.wispforest:owo-sentinel:${project.owo_version}"

//    modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-neoforge:${project.rei_version}"

    include modApi("io.wispforest.lavender-md-neo:core:${project.lavender_md_version}")
    include modApi("io.wispforest.lavender-md-neo:owo-ui:${project.lavender_md_version}")

//    modLocalRuntime "com.terraformersmc:modmenu:${project.modmenu_version}"

    // forge weirdness
    forgeRuntimeLibrary("io.wispforest:endec:0.1.3")
    forgeRuntimeLibrary("io.wispforest.endec:netty:0.1.2")
    forgeRuntimeLibrary("io.wispforest.endec:gson:0.1.2")
    //include api(forgeRuntimeLibrary("io.wispforest.endec:jankson:0.1.2"))
    forgeRuntimeLibrary("blue.endless:jankson:1.2.2")


    // --- testmod ---

    testmodImplementation sourceSets.main.output
}

base {
    archivesName = project.archives_base_name
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version
    }
}

//tasks.withType(JavaCompile).configureEach {
//    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
//    it.options.release = 17
//}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

remapJar {
    atAccessWideners.add("lavender.accesswidener")
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

def ENV = System.getenv()
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url ENV.MAVEN_URL
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}
